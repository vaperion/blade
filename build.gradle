plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.freefair.lombok' version '6.4.3'
}

allprojects {
    repositories {
        mavenCentral()
    }

    group = 'me.vaperion.blade'
    version = '3.0.7'

    // workaround for gradle issue: https://github.com/gradle/gradle/issues/17236#issuecomment-894385386
    tasks.withType(Copy).all {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        outputs.upToDateWhen { false }
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    afterEvaluate {
        shadowJar {
            archiveClassifier.set('')
            archiveFileName = 'blade-' + project.name + ".jar"
        }

        publishing {
            publications {
                maven(MavenPublication) {
                    artifact shadowJar
                }
            }
        }

        tasks.build.dependsOn(shadowJar)

        if (project.name != 'core') {
            tasks.shadowJar.dependsOn ':core:shadowJar'
        }
    }
}
